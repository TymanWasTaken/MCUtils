buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
    }
    dependencies {
        classpath "com.github.asbyth:forgegradle:8708bf3e01"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.5"
group= "com.tyman.mcutils" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "mcutils"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_22"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    clientRunArgs += "--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker"
}

configurations {
    lib // A way to specify which dependencies to include in the jar
    implementation.extendsFrom(lib)
}

repositories {
    maven {
        name = "Sk1er"
        url = "https://repo.sk1er.club/repository/maven-public/"
    }
    // The following are needed or gradle gets mad because it can't find essential's dependencies
    maven {
        name = "Jitpack"
        url = "https://jitpack.io/"
    }
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    lib('com.fathzer:javaluator:3.0.3')
    lib('com.vdurmont:semver4j:3.1.0')
    lib('com.github.kevinsawicki:http-request:6.0')
    lib('gg.essential:loader-launchwrapper:1.1.1')
    implementation('gg.essential:Essential:1194-10809-SNAPSHOT')
}

jar {
    manifest {
        attributes  "ModSide": "CLIENT",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker"
    }
    from {
        configurations.lib
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}
moveResources.dependsOn processResources
classes.dependsOn moveResources